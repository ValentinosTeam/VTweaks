plugins {
    id 'java'
}

description = 'A plugin with a collection of features that would be nice to have on our server, but are not significant enough to have their own plugin. The idea is to have a config file with a bunch of boolean variables to turn on or off features. '
group = 'gg.valentinos'
version = '0.2.1'

repositories {
    mavenCentral()
    maven { url "https://repo.papermc.io/repository/maven-public/" }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT"
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}
processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

tasks.register('buildAndRestartDocker') {
    dependsOn build

    doLast {
        def pluginPath = System.getenv("MC_PLUGIN_DIR")
        if (pluginPath == null) {
            throw new GradleException("Environment variable MC_PLUGIN_DIR is not set.")
        }
        def dockerContainer = System.getenv("DOCKER_CONTAINER")

        def pluginFile = file("build/libs/VTweaks-${version}.jar")
        def outputDir = file(pluginPath)

        copy {
            from pluginFile
            into outputDir
        }

        exec {
            commandLine 'docker', 'restart', dockerContainer
            ignoreExitValue = true
        }
    }
}